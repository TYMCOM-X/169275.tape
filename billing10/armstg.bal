PROCEDURE ARMY.STORAGE.BALANCING

BEGIN

FIELDS
MISC.AMT        SAME AS AMT,
ARMY.AMT        SAME AS AMT

RELATION ARMY.TMP IS
KEY     UUN
DATA    AMT

RELATION MIS.TMP SAME AS ARMY.TMP

RELATION DIFF.TMP IS
KEY     UUN
DATA    MISC.AMT, ARMY.AMT, USR.NAM

REPORT TO 'DS8207-ANY'
PAGE.SIZE 0

FOR EACH ARMY.STORAGE
IF PROD.CODE = 60 THEN
  SELECT ARMY.TMP VIA KEY THEN
    BEGIN
      ADD AMT OF ARMY.STORAGE TO AMT
      ALTER ARMY.TMP
    END
  ELSE
    INSERT INTO ARMY.TMP

FOR EACH ARMY.TMP
  FOR EACH MISC.TRAN VIA (UUN)
    IF PROD.CODE = 60 THEN
      SELECT MIS.TMP VIA KEY THEN
        BEGIN
          ADD AMT OF MISC.TRAN TO AMT
          ALTER MIS.TMP
        END
      ELSE
        INSERT INTO MIS.TMP USING (AMT OF MISC.TRAN FOR AMT)

FOR EACH ARMY.TMP
  FOR EACH MIS.TMP VIA (UUN)
    BEGIN
        IF AMT OF ARMY.TMP = AMT OF MIS.TMP THEN
          NOTHING
        ELSE
          INSERT INTO DIFF.TMP USING (AMT OF MIS.TMP FOR MISC.AMT,
          AMT OF ARMY.TMP FOR ARMY.AMT)
    END


TYPE COUNT(DIFF.TMP), @CR, @CR

FOR EACH DIFF.TMP
  FOR EACH UUN.USR.NAM VIA (UUN = UUN OF DIFF.TMP)
    ALTER DIFF.TMP USING (USR.NAM OF UUN.USR.NAM FOR USR.NAM)


FOR EACH DIFF.TMP WRITE REPORT
BEGIN
PRINT USR.NAM, (MISC.AMT-ARMY.AMT) AS '-6ZN.NN', @CR
END


END
