%   VALDAT.CLEANUP                     VALCLN.PR1      01  %
PROCEDURE VALDAT.CLEANUP  %           By Andrew M. Carrell Jun 16, 1980

*
THIS PROCEDURE REMOVES STOPPED RECORDS FROM THE CUSTOMER USER
RELATION (VAL) PRIOR TO A PRESELECTED DATE AND LOADS THEM INTO PERMANENT
HISTORY SYMBOLIC FILES IN UAS.
%

BEGIN %1%

FIELDS

        COUNT.HIS               AS '6Z',
        COUNT.TMP               AS '6Z',
        COUNT.VAL               AS '6Z',
        CUTOFF.DATE             AS DATE 'YYMMDD',
        REPLY                   AS 'C',
        TIME.HR.L               AS '2Z',
        TIME.MIN.L              AS '2N'

RELATION VAL.TMP SAME AS VAL

RELATION VAL.HIS.TMP SAME AS VAL


FORMAT TERMINAL.OUTPUT.HEADING
        "***BEGINNING PROCEDURE***",@SKIP 1,
        "VAL DATABASE CLEANUP",
        "...........VERSION 1.0 "

FORMAT TERMINAL.RUN.DATE.TIME
        TODAY AS DATE "MM/DD/YY",@TAB 1,
        TIME.HR.L,":",
        TIME.MIN.L,@SKIP 2

REPORT VALDAT.HIS TO "*"
   PAGE.SIZE 0



%****************INITIALIZATION****************%

MOVE DEFAULT TO
        COUNT.HIS,
        COUNT.TMP,
        COUNT.VAL,
        CUTOFF.DATE,
        REPLY
LET TIME.HR.L=(TIME/3600) AS "2Z"
LET TIME.MIN.L=((TIME-TIME/3600*3600)/60) AS "2N"
TYPE TERMINAL.OUTPUT.HEADING,TERMINAL.RUN.DATE.TIME,@SKIP 1

WHILE CUTOFF.DATE = DEFAULT DO
   BEGIN %5%
   TYPE 'INPUT CUTOFF DATE:'
   ACCEPT CUTOFF.DATE
   TYPE 'IS THIS THE CORRECT CUTOFF DATE-',CUTOFF.DATE,'? (Y/N):'
   ACCEPT REPLY
   IF REPLY NE 'Y' THEN
     MOVE DEFAULT TO CUTOFF.DATE
   ELSE
     NOTHING
   END %5%

FOR EACH VAL
   BEGIN %10%
   IF DSTOP LT CUTOFF.DATE THEN
     INSERT INTO VAL.HIS.TMP
   ELSE
     INSERT INTO VAL.TMP
   END %10%

MOVE COUNT(VAL) AS '6Z' TO COUNT.VAL
MOVE COUNT(VAL.TMP) AS '6Z' TO COUNT.TMP
MOVE COUNT(VAL.HIS.TMP) AS '6Z' TO COUNT.HIS


IF (COUNT.HIS + COUNT.TMP) = COUNT.VAL THEN
   BEGIN %15%
   FOR EACH VAL.HIS.TMP WRITE REPORT VALDAT.HIS
     PRINT TO VALDAT.HIS 
     UUN                ,",",
     ACTG.SYS.TYP       ,",",
     HOST               ,",",
     DSTART             ,",",
     DSTOP              ,@CR

     DELETE FROM VAL ALL
   FOR EACH VAL.TMP
     INSERT INTO VAL
   END %15%
ELSE
   BEGIN %20%
   TYPE 'VAL RECORDS NOT EQUAL CONTACT DBA',@CR
   ABORT
   END %20%
END %1%
