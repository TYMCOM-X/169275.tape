%   UASDAT.CLEANUP                     UASCLN.PR1      01  %
PROCEDURE UASDAT.CLEANUP  %           By Andrew M. Carrell Jun 16, 1980

*
THIS PROCEDURE REMOVES STOPPED RECORDS FROM THE CUSTOMER USER
RELATION (USER.ACTG.SYS) PRIOR TO A PRESELECTED DATE AND LOADS
THEM INTO PERMANENT HISTORY SYMBOLIC FILES IN UAS.
%

BEGIN %1%

FIELDS

        COUNT.HIS               AS '6Z',
        COUNT.TMP               AS '6Z',
        COUNT.UAS               AS '6Z',
        CUTOFF.DATE             AS DATE 'YYMMDD',
        REPLY                   AS 'C',
        TIME.HR.L               AS '2Z',
        TIME.MIN.L              AS '2N'

RELATION USER.ACTG.SYS.TMP SAME AS USER.ACTG.SYS

RELATION USER.ACTG.SYS.HIS.TMP SAME AS USER.ACTG.SYS


FORMAT TERMINAL.OUTPUT.HEADING
        "***BEGINNING PROCEDURE***",@SKIP 1,
        "USER.ACTG.SYS DATABASE CLEANUP",
        "...........VERSION 1.0 "

FORMAT TERMINAL.RUN.DATE.TIME
        TODAY AS DATE "MM/DD/YY",@TAB 1,
        TIME.HR.L,":",
        TIME.MIN.L,@SKIP 2

REPORT UASDAT.HIS TO "*"
   PAGE.SIZE 0




%****************INITIALIZATION****************%

MOVE DEFAULT TO
        COUNT.HIS,
        COUNT.TMP,
        COUNT.UAS,
        CUTOFF.DATE,
        REPLY
LET TIME.HR.L=(TIME/3600) AS "2Z"
LET TIME.MIN.L=((TIME-TIME/3600*3600)/60) AS "2N"
TYPE TERMINAL.OUTPUT.HEADING,TERMINAL.RUN.DATE.TIME,@SKIP 1

WHILE CUTOFF.DATE = DEFAULT DO
   BEGIN %5%
   TYPE 'INPUT CUTOFF DATE:'
   ACCEPT CUTOFF.DATE
   TYPE 'IS THIS THE CORRECT CUTOFF DATE-',CUTOFF.DATE,'? (Y/N):'
   ACCEPT REPLY
   IF REPLY NE 'Y' THEN
     MOVE DEFAULT TO CUTOFF.DATE
   ELSE
     NOTHING
   END %5%

FOR EACH USER.ACTG.SYS
   BEGIN %10%
   IF DSTOP LT CUTOFF.DATE THEN
     INSERT INTO USER.ACTG.SYS.HIS.TMP
   ELSE
     INSERT INTO USER.ACTG.SYS.TMP
   END %10%

MOVE COUNT(USER.ACTG.SYS) AS '6Z' TO COUNT.UAS
MOVE COUNT(USER.ACTG.SYS.TMP) AS '6Z' TO COUNT.TMP
MOVE COUNT(USER.ACTG.SYS.HIS.TMP) AS '6Z' TO COUNT.HIS

TYPE 'TOTAL USER.ACTG.SYS RECORDS-',COUNT.UAS,@SKIP 1
TYPE 'TOTAL USER.ACTG.SYS HISTORY RECORDS-',COUNT.HIS,@SKIP 1
TYPE 'TOTAL USER.ACTG.SYS TEMPORY RECORDS-',COUNT.TMP,@SKIP 1

IF (COUNT.HIS + COUNT.TMP) = COUNT.UAS THEN
   BEGIN %15%
   FOR EACH USER.ACTG.SYS.HIS.TMP WRITE REPORT UASDAT.HIS
   PRINT TO UASDAT.HIS
   UUN                  ,",",
   ACTG.SYS.TYP         ,",",
   DSTART               ,",",
   USR.SLSM             ,",",
   TRK.CODE             ,",",
   PRIC.CODE            ,",",
   USR.DET.CODE         ,",",
   PRIME.CODE           ,",",
   USR.TZ               ,",",
   DSTOP                ,",",
   HOLIDAY.CODE         ,",",
   RES.MIN              ,",",
   DST                  ,",",
   MAP.CODE             ,@CR

   DELETE FROM USER.ACTG.SYS ALL
   FOR EACH USER.ACTG.SYS.TMP
   INSERT INTO USER.ACTG.SYS
   END %15%
ELSE
   BEGIN %20%
   TYPE 'USER.ACTG.SYS RECORDS NOT EQUAL CONTACT DBA',@CR
   ABORT
   END %20%
END %1%
